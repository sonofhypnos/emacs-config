#+TITLE: Doom Configuration
* Introduction
* TODO write proper introduction
We stand on the shoulders of giants, nothing is original and all that. Here are the configurations that I've learned and [[https://www.amazon.de/dp/0761169253/ref=nosim?linkCode=gs2&tag=kleon2007d-21][stolen]] from the most:
- [[https://zzamboni.org/post/my-doom-emacs-configuration-with-commentary/][Diego Zamboni’s emacs configuration]] including his ebook on [[https://leanpub.com/lit-config][literate configuration]].
- [[https://github.com/tecosaur][Tecosaur’s emacs configuration]]  

* Config

My personal information
#+begin_src emacs-lisp
(setq user-full-name "Tassilo Neubauer"
      user-mail-address "tassilo.neubauer@gmail.com")
#+end_src

* TODO Figure out what loads org early
Setting up my directories.
#+begin_src emacs-lisp
(setq   org-directory "~/org/"
        org-roam-directory "~/org-roam/"
        projectile-project-search-path '("~/repos" "~/Dropbox/")
        zot-bib "~/repos/bibliography/zotLib.bib")
#+end_src

Set up multiple languages for spell-checking. See [[https://github.com/munen/emacs.d ][this]] repo for more info.
#+begin_src emacs-lisp
(after! ispell
  ;; Configure `LANG`, otherwise ispell.el cannot find a 'default
  ;; dictionary' even though multiple dictionaries will be configured
  ;; in next line.
  (setenv "LANG" "en_US.UTF-8")
  (setq ispell-program-name "hunspell")
  ;; Configure German, Swiss German, and two variants of English.
  (setq ispell-dictionary "de_DE,en_GB,en_US")
  ;; ispell-set-spellchecker-params has to be called
  ;; before ispell-hunspell-add-multi-dic will work
  (ispell-set-spellchecker-params)
  (ispell-hunspell-add-multi-dic "de_DE,en_GB,en_US")
  ;; For saving words to the personal dictionary, don't infer it from
  ;; the locale, otherwise it would save to ~/.hunspell_de_DE.
  (setq ispell-personal-dictionary "~/.hunspell_personal")
  (unless (file-exists-p ispell-personal-dictionary)
  (write-region "" nil ispell-personal-dictionary nil 0)))
;; The personal dictionary file has to exist, otherwise hunspell will
;; silently not use it.
#+end_src

** [[https://apps.ankiweb.net/][Anki]] setup

See [[https://yiufung.net/post/anki-org/][this excellent post]] to learn more about how to connect Anki with Emacs.
#+begin_src emacs-lisp
(map! :after anki-editor
      :map org-mode-map
        "<f12>"  #'anki-editor-cloze-region-auto-incr
        "<f11>"  #'anki-editor-cloze-region-dont-incr
        "<f10>"  #'anki-editor-reset-cloze-number
        "<f9>"   #'anki-editor-push-tree)
#+end_src

#+begin_src emacs-lisp
(use-package! anki-editor
  :after org-roam
  #+end_src

keybindings for Anki.
#+begin_src emacs-lisp
  :hook (org-capture-after-finalize . anki-editor-reset-cloze-number) ; Reset cloze-number after each capture.
  #+end_src
* TODO Figure out why mathjax is not working.
  Export Mathjax instead of Latex and use org-tags
  #+begin_src emacs-lisp
  :config
  (setq-default anki-editor-use-math-jax t)
  (setq anki-editor-org-tags-as-anki-tags t)
#+end_src

These functions where copied from [[https://yiufung.net/post/anki-org/][this post on how to use Anki with org]].
  #+begin_src emacs-lisp
  (defun anki-editor-cloze-region-auto-incr (&optional arg)
    "Cloze region without hint and increase card number."
    (interactive)
    (anki-editor-cloze-region my-anki-editor-cloze-number "")
    (setq my-anki-editor-cloze-number (1+ my-anki-editor-cloze-number))
    (forward-sexp))
  (defun anki-editor-cloze-region-dont-incr (&optional arg)
    "Cloze region without hint using the previous card number."
    (interactive)
    (anki-editor-cloze-region (cond ((eq my-anki-editor-cloze-number 1)
                                     (progn
                                       (setq my-anki-editor-cloze-number (1+ my-anki-editor-cloze-number))
                                       1))
                                    (t (1- my-anki-editor-cloze-number))) "")
    (forward-sexp))
  (defun anki-editor-reset-cloze-number (&optional arg)
    "Reset cloze number to ARG or 1"
    (interactive)
    (setq my-anki-editor-cloze-number (or arg 1)))
  (defun anki-editor-push-tree ()
    "Push all notes under a tree."
    (interactive)
    (anki-editor-push-notes '(4))
    (anki-editor-reset-cloze-number))
  ;; Initialize
  (anki-editor-reset-cloze-number))
  #+end_src

* Org
** Org-todo
Add custom confusion keyword to org-todo-keywords

#+begin_src emacs-lisp
(after! org
  (with-no-warnings
  (custom-declare-face '+org-todo-cancel  '((t (:inherit (bold error org-todo)))) "") ;; see dooms org module for more examples of how to do this.
  (custom-declare-face '+org-todo-project '((t (:inherit (bold font-lock-doc-face org-todo)))) ""))
#+end_src

** TODO Figure out why setting single stuff doesn't work?
 The following seemed like something that should work but didn't. I suspect it has something to do with the fact that the lists defined in dooms org module are immutable.

#+begin_src emacs-lisp :tangle no
(setcar org-todo-keywords (append (car org-todo-keywords) '("CON(c)"))) ;this code does not work!
(add-to-list 'org-todo-keyword-faces '("CON" . +org-todo-cancel))
#+end_src

In the end I just copied the stuff from the org-module and added my own keywords.
#+begin_src emacs-lisp
  (with-no-warnings
    (custom-declare-face '+org-todo-active  '((t (:inherit (bold font-lock-constant-face org-todo)))) "")
    (custom-declare-face '+org-todo-project '((t (:inherit (bold font-lock-doc-face org-todo)))) "")
    (custom-declare-face '+org-todo-onhold  '((t (:inherit (bold warning org-todo)))) "")
    (custom-declare-face '+org-todo-cancel  '((t (:inherit (bold error org-todo)))) ""))
  (setq org-todo-keywords
        '((sequence   ; Not sure what the sequence is doing here (where it gets evaluated?)
           "TODO(t)"  ; A task that needs doing & is ready to do
           "PROJ(P)"  ; A project, which usually contains other tasks
           "LOOP(r)"  ; A recurring task
           "STRT(s)"  ; A task that is in progress
           "WAIT(w)"  ; Something external is holding up this task
           "HOLD(h)"  ; This task is paused/on hold because of me
           "IDEA(i)"  ; An unconfirmed and unapproved task or notion
           "|"
           "DONE(d)"  ; Task successfully completed
           "KILL(k)") ; Task was cancelled, aborted or is no longer applicable
          (sequence
           "[ ](T)"   ; A task that needs doing
           "[-](S)"   ; Task is in progress
           "[?](W)"   ; Task is being held up or paused
           "[??](C)"  ; Confusion marker in notes
           "PRO(p)"  ; Pro in pro-con list
           "CON(c)"  ; Con in pro and con list
           "|"
           "[X](D)")  ; Task was completed
          (sequence
           "|"
           "OKAY(o)"
           "YES(y)"
           "NO(n)"))
        org-todo-keyword-faces
        '(("[-]"  . +org-todo-active)
          ("STRT" . +org-todo-active)
          ("[?]"  . +org-todo-onhold)
          ("WAIT" . +org-todo-onhold)
          ("HOLD" . +org-todo-onhold)
          ("PRO" . +org-todo-onhold)
          ("CON" . +org-todo-cancel)
          ("PROJ" . +org-todo-project)
          ("NO"   . +org-todo-cancel)
          ("KILL" . +org-todo-cancel))))
  #+end_src

** TODO Fix Capture
** TODO Enable Capture with Chrome

** Org-roam
** TODO Add Shortcuts for dailies
** TODO Repair Templates
** TODO Explain Org-roam configuration
#+begin_src emacs-lisp
(map! (:after org-roam
        :map org-mode-map
        :localleader
        :prefix "m"
        :desc "org-roam-dailies-goto-today" "t" #'org-roam-dailies-goto-today
        :desc "org-roam-extract-subtree" "x" #'org-roam-extract-subtree))
(after! org
  :config
  (setq org-export-with-tasks nil)
  (add-hook 'focus-out-hook
        (lambda () (org-save-all-org-buffers))))


(use-package! org-roam
  :after org
  #+end_src

My daily checklists
  #+begin_src emacs-lisp
:config
(setq daily-template
      (concat
       "#+title: %<%Y-%m-%d>\n* [/] Do Today\n* [/] Maybe Do Today"
       "\n* Morgenroutine"
       "\n - [ ] Kalender angesehen"
       "\n - [ ] Start tracking"
       "\n - [ ] Medis genommen"
       "\n - [ ] Uhr angezogen"
       "\n - [ ] Ziele gesetzt"
       "\n - [ ] Review Anki"
       "\n - [ ] Brush Teeth"
       "\n* Evening Routine"
       "\n - [ ] Check Habits/Beeminder"
       "\n - [ ] Tasks Reviewed"
       "\n - [ ] Timetracking Reviewed ([[id:4d96fd27-2523-475a-a791-a67f9996e5a4][Enter Deep Work]])"
       "\n - [ ] Anwer Journal Questions"
       "\n - [ ] Do active questions"
       "\n - [ ] Review Anki"
       "\n - [ ] Brush Teeth"
       "\n* Inbox"
       "\n* Journal"
       "\n* Evening Journal"
       "\n** What did you achieve today?"
       "\n** What are you grateful for?"
       "\n** What worried you today?"
       "\n** What else is on your mind?"))

(setq org-roam-dailies-directory "daily/")
(setq org-roam-dailies-capture-templates
      `(
        ("Journal" "daily" plain "%T\n%?\n"
         :if-new (file+head+olp "%<%Y-%m-%d>.org" ,daily-template ("Journal")))
       ))

(setq org-roam-capture-templates
      '(("d" "default" plain
         "%?"
         :if-new (file+head "${slug}.org"
                            "#+title: ${title}\n#+created: %<%y-%m-%d %H:%M>\n* Next\n* Related\n")
         :immediate-finish t
         :unnarrowed t)))
#+end_src

Some non-essential functions for org
#+begin_src emacs-lisp
(defun org-hide-properties ()
  "Hide all org-mode headline property drawers in buffer. Could be slow if it has a lot of overlays."
  (interactive)
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward
            "^ *:properties:\n\\( *:.+?:.*\n\\)+ *:end:\n" nil t)
      (let ((ov_this (make-overlay (match-beginning 0) (match-end 0))))
        (overlay-put ov_this 'display "")
        (overlay-put ov_this 'hidden-prop-drawer t))))
  (put 'org-toggle-properties-hide-state 'state 'hidden))

(defun org-show-properties ()
  "Show all org-mode property drawers hidden by org-hide-properties."
  (interactive)
  (remove-overlays (point-min) (point-max) 'hidden-prop-drawer t)
  (put 'org-toggle-properties-hide-state 'state 'shown))
(defun org-toggle-properties ()
  "Toggle visibility of property drawers."
  (interactive)
  (if (eq (get 'org-toggle-properties-hide-state 'state) 'hidden)
      (org-show-properties)
    (org-hide-properties)))
#+end_src

Adding org-hide-properties to the org-mode-hook, to hide properties of orgmode-files when visiting them.
#+begin_src emacs-lisp
(add-hook 'org-mode-hook #'org-hide-properties)
#+end_src

Make completion case insensitive in org-mode (and others, don't know how to make it org-mode-specific yet).
#+begin_src emacs-lisp
(defun completion-ignore-case-enable ()
    (setq completion-ignore-case t))
(add-hook 'org-mode-hook #'completion-ignore-case-enable)
#+end_src

* TODO Figure out how to enlarge window
* TODO Figure out why capture still isn't full screen
#+begin_src emacs-lisp
(defun tassilo/scratch-window-p ()
  (string= (substring-no-properties (cdr (assoc 'name (frame-parameters))))
                                    "_emacs scratchpad_"))
(defun tassilo/org-capture-cleanup ()
  "Delete capture windows if it is a scratch window"
  (and (tassilo/scratch-window-p)
       ;This worked for me opposed to just using just (delete-frame), so as long as it works I won't touch it (Similar use of progn below)
      (progn
        (progn
    (start-process "i3-msg" "*i3-msg*" "i3-msg" "scratchpad show")
    (org-roam-db-sync)
     (delete-frame))
     nil)))
(add-hook 'org-capture-after-finalize-hook #'tassilo/org-capture-cleanup)

(defun tassilo/org-capture-setup ()
  (and (tassilo/scratch-window-p)
       (progn
         (delete-other-windows)))) ;For some reason "progn" fixes both of my functions. I might want to find out why in the future, but for now I am happy it works at all.
(add-hook 'org-capture-mode-hook #'tassilo/org-capture-setup)
#+end_src

*** Setting up org-protocol for capture
I use org-protocol to take notes for the blog-posts/articles I am reading in the browser. I really like this workflow, but my problem with this is that it already broke time and time again and it was a real pain to find the culprit every time. Since then I have some Snippets ready in my config, if it is not working again for some reason which has been really helpful in debugging.

Here's the bindings I set up in [[https://github.com/tridactyl/tridactyl][tridactyl]] to trigger captures:
#+begin_src javascript :tangle no
bind rr js javascript:location.href ='org-protocol://capture?template=L&url='+ encodeURIComponent(location.href) + '&title=' + encodeURIComponent(document.title) + '&body=' + encodeURIComponent(window.getSelection())

bind rl js javascript:location.href = 'org-protocol://roam-ref?template=r&ref=' + encodeURIComponent(location.href) + '&title=' + encodeURIComponent(document.title) + '&body=' + encodeURIComponent(window.getSelection())
#+end_src


These are my other capture templates.
#+begin_src emacs-lisp
(require 'org-roam-protocol)

(setq org-my-anki-file (concat org-roam-directory "anki-stuff.org"))

(add-to-list 'org-capture-templates
             `("l" "Link" entry (file+headline ,(concat org-roam-directory "/20210510194711-read_and_take_notes.org") "Links")
               "* [[%:link][%:description]]\n %?\n \n %i\n%T"
               :immediate-finish t))
(add-to-list 'org-capture-templates
            '("a" "Anki basic"
                entry
                (file+headline org-my-anki-file "Dispatch Shelf")
                "* %<%y-%m-%d %H:%M>   %^g\n:PROPERTIES:\n:ANKI_NOTE_TYPE: Basic\n:ANKI_DECK: .main\n:END:\n** Front\n%?\n** Back\n%x\n"))
(add-to-list 'org-capture-templates
            '("A" "Anki cloze"
                entry
                (file+headline org-my-anki-file "Dispatch Shelf")
                "* %<%y-%m-%d %H:%M>   %^g\n:PROPERTIES:\n:ANKI_NOTE_TYPE: Cloze\n:ANKI_DECK: .main\n:END:\n** Text\n%?\n** Extra\n%f\n%x"))
(add-to-list 'org-capture-templates
            '("T" "Anki type"
                entry
                (file+headline org-my-anki-file "Dispatch Shelf")
                "* %<%y-%m-%d %H:%M>   %^g\n:PROPERTIES:\n:ANKI_NOTE_TYPE:1typing\n:ANKI_DECK: .main\n:END:\n** Text\n%?\n** Extra\n%x"))
(add-to-list 'org-capture-templates
             '("L" "Protocol Link" entry
               (file+headline +org-capture-notes-file "Inbox")
               "* [[%:link][%:description]] \n \n \n%i \n %T"
               :prepend t))
(add-to-list 'org-capture-templates
             '("S" "Todo Protocoll" entry
               (file+headline +org-capture-notes-file "Inbox")
               "* [[%:link][% \"%:description\"]] \n \n* TODO %? %i \n %T"
               :prepend t
               :kill-buffer t))

(setq org-roam-capture-ref-templates
      '(("r" "ref" plain
         "%u %?\n\n* \" %c\"  "
         :if-new (file+head "${slug}.org"
                            "#+title: ${title}\n#+author:\n")
         :unnarrowed t))))

 (defun make-capture-frame ()
     "Create a new frame and run org-capture."
     (interactive)
     (make-frame '((name . "capture")))
     (require 'noflet)
     (select-frame-by-name "capture")
     (delete-other-windows)
     (noflet ((switch-to-buffer-other-window (buf) (switch-to-buffer buf)))
       (org-capture)))

#+end_src


Org-roam-ui is useful for viewing your notes and their connections as a graph in the browser.
#+begin_src emacs-lisp
(use-package! websocket
    :after org-roam)

(use-package! org-roam-ui
    :after org-roam
    :config
    (setq org-roam-ui-sync-theme t
          org-roam-ui-follow t
          org-roam-ui-update-on-save t
          org-roam-ui-open-on-start t))
#+end_src


Org-noter for pdfs. I am still searching for the pdf-annotation tool that I actually like using, but in the meantime I'll try to use org-noter.
#+begin_src emacs-lisp
(use-package! org-noter
  :after org
  :config
  (setq org-noter-notes-search-path '("~/org-roam/")))
#+end_src

#+begin_src emacs-lisp
(use-package! bibtex
  :init
(setq
   bibtex-completion-notes-path org-directory
   bibtex-completion-bibliography zot-bib
   bibtex-completion-pdf-field "file"
   bibtex-completion-notes-template-multiple-files
   (concat
    "#+TITLE: ${title}\n"
    "#+ROAM_KEY: cite:${=key=}\n"
    "* TODO Notes\n"
    ":PROPERTIES:\n"
    ":Custom_ID: ${=key=}\n"
    ":NOTER_DOCUMENT: %(orb-process-file-field \"${=key=}\")\n"
    ":AUTHOR: ${author-abbrev}\n"
    ":JOURNAL: ${journaltitle}\n"
    ":DATE: ${date}\n"
    ":YEAR: ${year}\n"
    ":DOI: ${doi}\n"
    ":URL: ${url}\n"
    ":END:\n\n")))

(use-package! org-ref
  :after org
  :config
  :init
  (setq org-ref-completion-library 'org-ref-ivy-cite
        org-ref-get-pdf-filename-function 'org-ref-get-pdf-filename-helm-bibtex)
  (setq
   org-ref-default-bibliography (list zot-bib)
   org-ref-bibliography-notes  (concat org-roam-directory "bibliography.org")
   org-ref-note-title-format "* TODO %y - %t\n :PROPERTIES:\n  :Custom_ID: %k\n  :NOTER_DOCUMENT: %F\n :ROAM_KEY: cite:%k\n  :AUTHOR: %9a\n  :JOURNAL: %j\n  :YEAR: %y\n  :VOLUME: %v\n  :PAGES: %p\n  :DOI: %D\n  :URL: %U\n :END:\n\n"
   org-ref-notes-directory (concat org-roam-directory "/lit")
   org-ref-notes-function 'orb-edit-notes))

(use-package! org-roam-bibtex
  :after org-roam
  :hook (org-roam-mode . org-roam-bibtex-mode)
  :config
  (setq org-roam-bibtex-preformat-keywords
        '("=key=" "title" "url" "file" "author-or-editor" "keywords"))
  (setq orb-templates
        '(("r" "ref" plain (function org-roam-capture--get-point)
           ""
           :file-name "${slug}"
           :head "#+TITLE: ${=key=}: ${title}\n#+ROAM_KEY: ${ref}\n#+ROAM_TAGS:
- keywords :: ${keywords}
\n* ${title}\n  :PROPERTIES:\n  :Custom_ID: ${=key=}\n  :URL: ${url}\n  :AUTHOR: ${author-or-editor}\n  :NOTER_DOCUMENT: %(orb-process-file-field \"${=key=}\")\n  :NOTER_PAGE: \n  :END:\n\n"
           :unnarrowed t))))

#+end_src

#+begin_src emacs-lisp
(use-package! org-pdftools
  :after org
  :hook (org-mode . org-pdftools-setup-link))

(use-package! org-noter-pdftools
  :after org-noter
  :config
  (pdf-tools-install)
  ;; Add a function to ensure precise note is inserted
  (defun org-noter-pdftools-insert-precise-note (&optional toggle-no-questions)
    (interactive "P")
    (org-noter--with-valid-session
     (let ((org-noter-insert-note-no-questions (if toggle-no-questions
                                                   (not org-noter-insert-note-no-questions)
                                                 org-noter-insert-note-no-questions))
           (org-pdftools-use-isearch-link t)
           (org-pdftools-use-freestyle-annot t))
       (org-noter-insert-note (org-noter--get-precise-info)))))
  ;; fix https://github.com/weirdNox/org-noter/pull/93/commits/f8349ae7575e599f375de1be6be2d0d5de4e6cbf
  (defun org-noter-set-start-location (&optional arg)
    "When opening a session with this document, go to the current location.
With a prefix ARG, remove start location."
    (interactive "P")
    (org-noter--with-valid-session
     (let ((inhibit-read-only t)
           (ast (org-noter--parse-root))
           (location (org-noter--doc-approx-location (when (called-interactively-p 'any) 'interactive))))
       (with-current-buffer (org-noter--session-notes-buffer session)
         (org-with-wide-buffer
          (goto-char (org-element-property :begin ast))
          (if arg
              (org-entry-delete nil org-noter-property-note-location)
            (org-entry-put nil org-noter-property-note-location
                           (org-noter--pretty-print-location location))))))))
  (with-eval-after-load 'pdf-annot
    (add-hook 'pdf-annot-activate-handler-functions #'org-noter-pdftools-jump-to-note)))
#+end_src

I use this shortcut a lot to make notes for my university courses
#+begin_src emacs-lisp
(use-package! org-download
  :after org
  :init
  (map! :leader
        :prefix "d"
        :desc "org-screenshot" "d" #'org-download-screenshot)
(add-hook 'dired-mode-hook 'org-download-enable)
(setq org-image-actual-width nil))
#+end_src


* Miscelanous
I copied this macro from [[https://stackoverflow.com/a/22418983/492140][here]] to get [[https://github.com/emacs-evil/evil-surround][evil-surround]] keybindings for these special characters: |/.*$.
#+begin_src emacs-lisp
(defmacro define-and-bind-quoted-text-object (name key start-regex end-regex)
  (let ((inner-name (make-symbol (concat "evil-inner-" name)))
        (outer-name (make-symbol (concat "evil-a-" name))))
    `(progn
       (evil-define-text-object ,inner-name (count &optional beg end type)
         (evil-select-paren ,start-regex ,end-regex beg end type count nil))
       (evil-define-text-object ,outer-name (count &optional beg end type)
         (evil-select-paren ,start-regex ,end-regex beg end type count t))
       (define-key evil-inner-text-objects-map ,key #',inner-name)
       (define-key evil-outer-text-objects-map ,key #',outer-name))))

(define-and-bind-quoted-text-object "pipe" "|" "|" "|")
(define-and-bind-quoted-text-object "slash" "/" "/" "/")
(define-and-bind-quoted-text-object "asterisk" "*" "*" "*")
(define-and-bind-quoted-text-object "dot" "." "\\." "\\.")
(define-and-bind-quoted-text-object "dollar" "$" "\\$" "\\$") ;; sometimes your have to escape the regex
#+end_src

Enabling night-mode for pdfs
#+begin_src emacs-lisp
(after! pdf-tools
  (add-hook! 'pdf-tools-enabled-hook
    (pdf-view-midnight-minor-mode 1)))
#+end_src

Set up bindings for org-recoll
#+begin_src emacs-lisp
(global-set-key (kbd "C-c g") 'org-recoll-search)
(global-set-key (kbd "C-c u") 'org-recoll-update-index)
#+end_src

backup files
#+begin_src emacs-lisp
(setq auto-save-default t
      make-backup-files t)
#+end_src

[[https://github.com/emacsmirror/spray][Spray]] provides Speed reading functionality for Emacs. It seemed really interesting and I see a small chance it might change my reading habits the same way as the [[https://chrome.google.com/webstore/detail/video-speed-controller/nffaoalbilbmmfgbnbgppjihopabppdk][video speed controller]] Browser extension has changed my viewing habits. So far I haven't really used it though.
#+begin_src emacs-lisp
(map! :after spray
      :map spray-mode-map
      "s" #'spray-slower
      "d" #'spray-faster
      "j" #'spray-backward-word
      "k" #'spray-stop
      "l" #'spray-forward-word
      "SPC" #'spray-stop
      "q" #'spray-quit)
#+end_src

After tangling, configuration and packages get recompiled by running ```doom compile```.

#+begin_src emacs-lisp
(defun tassilo/post-tangle-config ()
    (and (file-in-directory-p
        buffer-file-name doom-private-dir)
       (async-shell-command "cp config.org README.org && sed -i '/^[^\"]*TODO[^\"]*$/d' README.org")
       (start-process "compile-config" "*compile-config*" "compile-config.sh" "~/.doom.d")))

(defun tassilo/enable-post-tangle ()
  (add-hook 'after-save-hook #'tassilo/post-tangle-config nil 'local))

(after! org
  (add-hook 'org-mode-hook #'tassilo/enable-post-tangle))
(add-to-list 'display-buffer-alist '("*Async Shell Command*" display-buffer-no-window (nil)))
#+end_src

Setting the scratch buffer in doom
#+begin_src emacs-lisp
(after! emacs-lisp-mode
  (setq doom-scratch-initial-major-mode emacs-lisp-mode))
#+end_src

#+begin_src emacs-lisp
(use-package! org-auto-tangle
  :defer t
  :hook (org-mode . org-auto-tangle-mode)
  :config
  (setq org-auto-tangle-default t))
#+end_src

Set more finegrained undo and enable timetracking modes. I am currently using wakatime, but recently I discovered [[https://activitywatch.net/][activitywatch]], which being open source and all seems nicer than wakatime, but needs a bit more manual configuration to categorize activities.
#+begin_src emacs-lisp
(after! evil
  (setq evil-want-fine-undo t)
  (setq-default delete-by-moving-to-trash t)
  (global-wakatime-mode)
  (global-activity-watch-mode))
#+end_src

Add default pdf-viewer
#+begin_src emacs-lisp
(use-package! openwith
  :after-call pre-command-hook
  :config
 ;; (openwith-mode t) ;keeping openwith-mode disabled until I've found a solution for inline images
  (add-to-list 'openwith-associations '("\\.pdf\\'" "zathura" (file)))
  #+end_src

  The following is needed to keep openwith-mode from interfering with org-display-inline-images. See [[https://emacs.stackexchange.com/questions/3180/org-mode-cant-display-inline-images-when-openwith-mode-is-turned-on][this stackexchange question]] for more info.
#+begin_src emacs-lisp
    (defadvice org-display-inline-images
    (around handle-openwith
            (&optional include-linked refresh beg end) activate compile)
    (if openwith-mode
        (progn
            (openwith-mode -1)
            ad-do-it
            (openwith-mode 1))
        ad-do-it)))
#+end_src

From https://gist.github.com/mads-hartmann/3402786
#+begin_src emacs-lisp
(defun toggle-maximize-buffer () "Maximize buffer"
       (interactive)
       (if (= 1 (length (window-list)))
           (jump-to-register '_)
         (progn
           (window-configuration-to-register '_)
           (delete-other-windows))))
#+end_src


I don't really use the terminal in Emacs (yet). Most of this configuration was copied from [[https://github.com/daviwil/emacs-from-scratch/blob/f4918aadf6970b098999d28bdbc212942aa62b80/show-notes/Emacs-09.org][this repo]].
#+begin_src emacs-lisp
(use-package! vterm
  :after org
  :commands vterm
  :config
  (setq term-prompt-regexp "^[^#$%>\n]*[#$%>] *")
  (setq vterm-shell "zsh")
  (setq vterm-max-scrollback 10000))

(use-package! term
  :after org
  :config
  (setq explicit-shell-file-name "zsh")
  (setq explicit-zsh-args '()) ; I don't know what this is for?
  (setq term-prompt-regexp "^[^#$%>\n]*[#$%>] *"))

#+end_src

* TODO Fix remapping space
The way I remap these commands is definitely incorrect, and once I have time for it, i'll figure out the correct way.
#+begin_src emacs-lisp
(map! (:when (featurep! :tools lookup)
 :leader :desc "projectile find file" :r ":" #'projectile-find-file
 :leader :desc "execute emacs command" :r "SPC" #'execute-extended-command
 :leader :desc "helm-projectile-rg" :r "l" #'helm-projectile-rg))
  #+end_src

This bit is required in order for benchmark-init to stop complaining.
#+begin_src emacs-lisp
(cl-letf (((symbol-function 'define-obsolete-function-alias) #'defalias))
 (use-package benchmark-init
   :config
   (require 'benchmark-init-modes) ; explicitly required
   (add-hook 'after-init-hook #'benchmark-init/deactivate)))
#+end_src

This is to add a confusion marker in my notes.


I have this to make code blocks look nicer in org-mode
#+begin_src emacs-lisp
(setq-default prettify-symbols-alist '(("#+BEGIN_SRC" . "$")
                                       ("#+END_SRC" . "ℝ")
                                       ("#+RESULTS:" . "↦")
                                       ("#+begin_src" . "<<")
                                       ("#+end_src" . ">>")))
(setq prettify-symbols-unprettyfy-at-point 'rigth-edge)
(add-hook 'org-mode-hook 'prettify-symbols-mode)
#+end_src

Add refactoring shortcut.
#+begin_src emacs-lisp
(after! emr (define-key prog-mode-map (kbd "M-RET") 'em2r-show-refactor-menu))
#+end_src

Configure Rust.
#+begin_src emacs-lisp
(after! lsp-rust
  (setq lsp-rust-server 'rust-analyzer))
#+end_src

For some reason that I haven't figured out yet, spell-fu is highlighting every single word. In order to avoid this problem I've just completly disabled it for now.
#+begin_src emacs-lisp
(remove-hook 'text-mode-hook #'spell-fu-mode)
#+end_src

Enable evil-insert in monkeytype-mode

#+begin_src emacs-lisp
;;(defun my/monkeytype-mode-hook ()
;;  "Hooks for monkeytype-mode."
;;  (centered-cursor-mode)
;;  (evil-escape-mode -1)
;;  (evil-insert -1))
;;(add-hook 'monkeytype-mode-hook #'my/monkeytype-mode-hook)
#+end_src


Finally getting around to adding some auto-completion. For now just copy pasted from [[https://codingstruggles.com/emacs,/tabnine/setting-tabnine-emacs.html][this tutorial]].
#+begin_src emacs-lisp
(after! company
  (setq +lsp-company-backends '(company-tabnine :separate company-capf company-yasnippet))
  (setq company-show-numbers t)
  (setq company-idle-delay 0)
)
#+end_src

Experimenting with configuring org-agenda
